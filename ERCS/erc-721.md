---
eip: 721
title: Non-Fungible Token Standard
author: William Entriken (@fulldecent), Dieter Shirley <dete@axiomzen.co>, Jacob Evans <jacob@dekz.net>, Nastassia Sachs <nastassia.sachs@protonmail.com>
discussions-to: https://github.com/ethereum/eips/issues/721
type: Standards Track
category: ERC
status: Final
created: 2018-01-24
requires: 165
---

## Simple Summary

* goal
  * üí°standard interface -- for -- non-fungible tokens (== deeds) üí°

## Abstract

* enable
  * standard API's implementation -- for -- NFTs | smart contracts
    * _Example:_ wallet/broker/auction applications

* provides
  * basic functionality -- to -- track & transfer NFTs


* NFTs 
  * == ownership -- over -- digital OR physical assets
    * _Example:_ 
      - Physical property ‚Äî houses, unique artwork --
      - Virtual collectibles ‚Äî unique pictures of kittens, collectible cards --
      - "Negative value" assets ‚Äî loans, burdens and other responsibilities --
  * ownership
    * individual
    * TP operator management
  * *distinguishable*
    * -> you MUST track EACH NFT's ownership SEPARATELY

## Motivation

* inspired -- by -- ERC-20 token standard /
  * EIP-20 is NOT sufficient -- for -- tracking NFTs
    * Reason: üß†EACH asset is DISTINCT (non-fungible) & each of a quantity of tokens is IDENTICAL (fungible)üß†
    * needed -- by -- Wallets/brokers/auctioneers

## Specification

* The keywords "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "MAY", and "OPTIONAL" -- follow -- RFC 2119

* ‚ö†Ô∏èALL ERC-721 compliant contract -- MUST implement -- `ERC721` & `ERC165` interfaces‚ö†Ô∏è

```solidity
pragma solidity ^0.4.20;

/// @title ERC-721 Non-Fungible Token Standard
/// @dev See https://eips.ethereum.org/EIPS/eip-721
///  Note: the ERC-165 identifier for this interface is 0x80ac58cd.
interface ERC721 /* is ERC165 */ {
    /// @dev This emits when ownership of any NFT changes by any mechanism.
    ///  This event emits when NFTs are created (`from` == 0) and destroyed
    ///  (`to` == 0). Exception: during contract creation, any number of NFTs
    ///  may be created and assigned without emitting Transfer. At the time of
    ///  any transfer, the approved address for that NFT (if any) is reset to none.
    event Transfer(address indexed _from, address indexed _to, uint256 indexed _tokenId);

    /// @dev This emits when the approved address for an NFT is changed or
    ///  reaffirmed. The zero address indicates there is no approved address.
    ///  When a Transfer event emits, this also indicates that the approved
    ///  address for that NFT (if any) is reset to none.
    event Approval(address indexed _owner, address indexed _approved, uint256 indexed _tokenId);

    /// @dev This emits when an operator is enabled or disabled for an owner.
    ///  The operator can manage all NFTs of the owner.
    event ApprovalForAll(address indexed _owner, address indexed _operator, bool _approved);

    /// @notice Count all NFTs assigned to an owner
    /// @dev NFTs assigned to the zero address are considered invalid, and this
    ///  function throws for queries about the zero address.
    /// @param _owner An address for whom to query the balance
    /// @return The number of NFTs owned by `_owner`, possibly zero
    function balanceOf(address _owner) external view returns (uint256);

    /// @notice Find the owner of an NFT
    /// @dev NFTs assigned to zero address are considered invalid, and queries
    ///  about them do throw.
    /// @param _tokenId The identifier for an NFT
    /// @return The address of the owner of the NFT
    function ownerOf(uint256 _tokenId) external view returns (address);

    /// @notice Transfers the ownership of an NFT from one address to another address
    /// @dev Throws unless `msg.sender` is the current owner, an authorized
    ///  operator, or the approved address for this NFT. Throws if `_from` is
    ///  not the current owner. Throws if `_to` is the zero address. Throws if
    ///  `_tokenId` is not a valid NFT. When transfer is complete, this function
    ///  checks if `_to` is a smart contract (code size > 0). If so, it calls
    ///  `onERC721Received` on `_to` and throws if the return value is not
    ///  `bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`.
    /// @param _from The current owner of the NFT
    /// @param _to The new owner
    /// @param _tokenId The NFT to transfer
    /// @param data Additional data with no specified format, sent in call to `_to`
    function safeTransferFrom(address _from, address _to, uint256 _tokenId, bytes data) external payable;

    /// @notice Transfers the ownership of an NFT from one address to another address
    /// @dev This works identically to the other function with an extra data parameter,
    ///  except this function just sets data to "".
    /// @param _from The current owner of the NFT
    /// @param _to The new owner
    /// @param _tokenId The NFT to transfer
    function safeTransferFrom(address _from, address _to, uint256 _tokenId) external payable;

    /// @notice Transfer ownership of an NFT -- THE CALLER IS RESPONSIBLE
    ///  TO CONFIRM THAT `_to` IS CAPABLE OF RECEIVING NFTS OR ELSE
    ///  THEY MAY BE PERMANENTLY LOST
    /// @dev Throws unless `msg.sender` is the current owner, an authorized
    ///  operator, or the approved address for this NFT. Throws if `_from` is
    ///  not the current owner. Throws if `_to` is the zero address. Throws if
    ///  `_tokenId` is not a valid NFT.
    /// @param _from The current owner of the NFT
    /// @param _to The new owner
    /// @param _tokenId The NFT to transfer
    function transferFrom(address _from, address _to, uint256 _tokenId) external payable;

    /// @notice Change or reaffirm the approved address for an NFT
    /// @dev The zero address indicates there is no approved address.
    ///  Throws unless `msg.sender` is the current NFT owner, or an authorized
    ///  operator of the current owner.
    /// @param _approved The new approved NFT controller
    /// @param _tokenId The NFT to approve
    function approve(address _approved, uint256 _tokenId) external payable;

    /// @notice Enable or disable approval for a third party ("operator") to manage
    ///  all of `msg.sender`'s assets
    /// @dev Emits the ApprovalForAll event. The contract MUST allow
    ///  multiple operators per owner.
    /// @param _operator Address to add to the set of authorized operators
    /// @param _approved True if the operator is approved, false to revoke approval
    function setApprovalForAll(address _operator, bool _approved) external;

    /// @notice Get the approved address for a single NFT
    /// @dev Throws if `_tokenId` is not a valid NFT.
    /// @param _tokenId The NFT to find the approved address for
    /// @return The approved address for this NFT, or the zero address if there is none
    function getApproved(uint256 _tokenId) external view returns (address);

    /// @notice Query if an address is an authorized operator for another address
    /// @param _owner The address that owns the NFTs
    /// @param _operator The address that acts on behalf of the owner
    /// @return True if `_operator` is an approved operator for `_owner`, false otherwise
    function isApprovedForAll(address _owner, address _operator) external view returns (bool);
}

interface ERC165 {
    /// @notice Query if a contract implements an interface
    /// @param interfaceID The interface identifier, as specified in ERC-165
    /// @dev Interface identification is specified in ERC-165. This function
    ///  uses less than 30,000 gas.
    /// @return `true` if the contract implements `interfaceID` and
    ///  `interfaceID` is not 0xffffffff, `false` otherwise
    function supportsInterface(bytes4 interfaceID) external view returns (bool);
}
```

* if wallet/broker/auction application will accept safe transfers -> MUST implement the wallet interface
  ```solidity
  /// @dev Note: the ERC-165 identifier for this interface is 0x150b7a02.
  interface ERC721TokenReceiver {
      /// @notice Handle the receipt of an NFT
      /// @dev The ERC721 smart contract calls this function on the recipient
      ///  after a `transfer`. This function MAY throw to revert and reject the
      ///  transfer. Return of other than the magic value MUST result in the
      ///  transaction being reverted.
      ///  Note: the contract address is always the message sender.
      /// @param _operator The address which called `safeTransferFrom` function
      /// @param _from The address which previously owned the token
      /// @param _tokenId The NFT identifier which is being transferred
      /// @param _data Additional data with no specified format
      /// @return `bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`
      ///  unless throwing
      function onERC721Received(address _operator, address _from, uint256 _tokenId, bytes _data) external returns(bytes4);
  }
  ```

### **metadata extension**
* OPTIONAL
* allows 
  * your smart contract -- is interrogated for -- 
    * its name
    * details -- about the -- assets / your NFTs represent
  ```solidity
  /// @title ERC-721 Non-Fungible Token Standard, optional metadata extension
  /// @dev See https://eips.ethereum.org/EIPS/eip-721
  ///  Note: the ERC-165 identifier for this interface is 0x5b5e139f.
  interface ERC721Metadata /* is ERC721 */ {
      /// @notice A descriptive name for a collection of NFTs in this contract
      function name() external view returns (string _name);
  
      /// @notice An abbreviated name for NFTs in this contract
      function symbol() external view returns (string _symbol);
  
      /// @notice A distinct Uniform Resource Identifier (URI) for a given asset.
      /// @dev Throws if `_tokenId` is not a valid NFT. URIs are defined in RFC
      ///  3986. The URI may point to a JSON file that conforms to the "ERC721
      ///  Metadata JSON Schema".
      function tokenURI(uint256 _tokenId) external view returns (string);
  }
  ```
  ```json
  {
      "title": "Asset Metadata",
      "type": "object",
      "properties": {
          "name": {
              "type": "string",
              "description": "Identifies the asset to which this NFT represents"
          },
          "description": {
              "type": "string",
              "description": "Describes the asset to which this NFT represents"
          },
          "image": {
              "type": "string",
              "description": "A URI pointing to a resource with mime type image/* representing the asset to which this NFT represents. Consider making any images at a width between 320 and 1080 pixels and aspect ratio between 1.91:1 and 4:5 inclusive."
          }
      }
  }
  ```

### * **enumeration extension**
* OPTIONAL
* allows,
  * your contract -- publish -- it's full list of NFTs
  * your contract's NFTs are discoverable
  ```solidity
  /// @title ERC-721 Non-Fungible Token Standard, optional enumeration extension
  /// @dev See https://eips.ethereum.org/EIPS/eip-721
  ///  Note: the ERC-165 identifier for this interface is 0x780e9d63.
  interface ERC721Enumerable /* is ERC721 */ {
      /// @notice Count NFTs tracked by this contract
      /// @return A count of valid NFTs tracked by this contract, where each one of
      ///  them has an assigned and queryable owner not equal to the zero address
      function totalSupply() external view returns (uint256);
  
      /// @notice Enumerate valid NFTs
      /// @dev Throws if `_index` >= `totalSupply()`.
      /// @param _index A counter less than `totalSupply()`
      /// @return The token identifier for the `_index`th NFT,
      ///  (sort order not specified)
      function tokenByIndex(uint256 _index) external view returns (uint256);
  
      /// @notice Enumerate NFTs assigned to an owner
      /// @dev Throws if `_index` >= `balanceOf(_owner)` or if
      ///  `_owner` is the zero address, representing invalid NFTs.
      /// @param _owner An address where we are interested in NFTs owned by them
      /// @param _index A counter less than `balanceOf(_owner)`
      /// @return The token identifier for the `_index`th NFT assigned to `_owner`,
      ///   (sort order not specified)
      function tokenOfOwnerByIndex(address _owner, uint256 _index) external view returns (uint256);
  }
  ```

### Caveats

* Solidity 0.4.20
  * NOT expressive enough -- to -- document the ERC-721 standard

* contract / complies with ERC-721
  * MUST ALSO comply
    - Solidity issue #3412
      - interfaces include EXPLICIT mutability guarantees / EACH function
        - Mutability guarantees / ordered weak -- to -> strong
          - `payable`, 
          - implicit nonpayable,
          - `view`,
          - `pure`
      - your implementation -- MUST meet the -- mutability guarantee | this interface & you -- MAY meet a -- stronger guarantee
      - _Example:_ `payable` function | this interface may be implemented -- as -- nonpayable | your contract
      - | NEXT Solidity release,
        - allow your stricter contract inherit -- from -- this interface
      - | Solidity 0.4.20,
        - BEFORE inheriting from your contract, 
          - you can edit this interface -- to -- add stricter mutability 
    - Solidity issue #3419
      - contract / implements `ERC721Metadata` or `ERC721Enumerable` -- SHALL ALSO -- implement `ERC721`
      - ERC-721 -- implements the -- requirements of interface ERC-165
    - Solidity issue #2330
      - if | this specification, function is shown -- as -- `external` & contract uses `public` visibility -> contract is compliant 
      - | Solidity 0.4.20,
        - workaround
          - == BEFORE inheriting from your contract, edit this interface -- to switch to -- `public` 
    - Solidity issues #3494, #3544
      - if you use `this.*.selector` 
        - | Solidity 0.4.20,
          - marked as a warning
        - | future Solidity versions,
          - NOT marked as an error

## Rationale

* uses of Ethereum smart contracts / -- depend on -- tracking distinguishable assets
  * NOWADAYS
    * LAND | Decentraland,
    * eponymous punks | CryptoPunks,
    * in-game items -- via -- systems (DMarket or EnjinCoin)
  * POSSIBLE FUTURE
    * tracking real-world assets
      * _Example:_ real-estate (Ubitquity or Propy)
  * requirements
    * ownership individually & atomically tracked / EACH asset
      * != "lumped together" the items 

* goal
  * standardized interface / allows for cross-functional asset management & sales platforms

### **"NFT" Word Choice**

* satisfactory to nearly everyone surveyed
* widely applicable | broad universe of distinguishable digital assets

* ALTERNATIVES considered
  * distinguishable asset,
  * title,
  * token,
  * asset,
  * equity,
  * ticket

### **NFT Identifiers**

* == unique `uint256` ID | ERC-721 smart contract
  * ‚ùåSHALL NOT change | life of the contract ‚ùå
  * ‚ùåNOT have SPECIFIC pattern‚ùå
    * == ‚ùåcallers SHALL NOT assume ‚ùå
      * == callers MUST treat the ID -- as a -- "black box"
    * _Example:_ start with ID 0 & simply increment by 1 / EACH NEW NFT 
  * [enumeration](#specification) interface
  * allows
    * wide variety of applications
      * Reason: üß†UUIDs & sha3 hashes -- are directly convertible to -- `uint256`üß† 

* `(contract address, uint256 tokenId)`
  * globally unique
  * fully-qualified identifier / specific asset | Ethereum chain

### **Transfer Mechanism**

* [`safeTransferFrom`](#specification)
  * overloaded WITH & WITHOUT a `bytes` parameter
* `transferFrom`
  * unsafe

* ways to initiate Transfers
  - owner of an NFT
  - approved address of an NFT
  - current owner of an NFT's authorized operator 

* authorized operator -- may set -- the approved address for an NFT
  * uses
    * üëÄwallet, broker & auction applications can quickly use a LARGE number of NFTsüëÄ

* `transfer` & `accept` functions'
  * documentation
    * ONLY specify conditions, | transaction MUST throw
  * `transfer` implementationS
    * | OTHER situations, MAY also throw -> allows
      - **if the contract is paused -> disallow transfers**
        - prior art, CryptoKitties deployed contract, line 611
      - **Blocklist certain address -- from -- receiving NFTs**
        - prior art, CryptoKitties deployed contract, lines 565, 566
      - **Disallow unsafe transfers** 
        - `transferFrom` throws UNLESS
          - `_to` == `msg.sender` OR
          - `countOf(_to)` == OR was non-zero
      - **Charge a fee to both parties of a transaction**
        - if it was previously the zero address -> | calling `approve` with a non-zero `_approved`, require payment   
        - TODO: refund payment if calling `approve` with the zero address if it was previously a non-zero address, require payment when calling any transfer function, require transfer parameter `_to` to equal `msg.sender`, require transfer parameter `_to` to be the approved address for the NFT
      - **Read only NFT registry**
        - ALWAYS throw -- from -- `safeTransferFrom`, `transferFrom`, `approve` and `setApprovalForAll`

* Failed transactions -- will -- throw
  * Reason: üß†best practiceüß†
  * [ERC-223](erc-223.md),
  * [ERC-677](https://github.com/ethereum/EIPs/issues/677),
  * [ERC-827](https://github.com/ethereum/EIPs/issues/827)
  * and OpenZeppelin's implementation of SafeERC20.sol

* [ERC-20](erc-20.md)
  * üí°defined an `allowance` feature üí°
    * | this caused a problem when called and then later modified to a different amount, as on OpenZeppelin issue #438
* NO allowance
  * Reason: üß†every NFT is unique & the quantity is none or one üß†
  * -> ERC-20's design's benefits WITHOUT discovered problems

* creation of NFTs ("minting") & destruction of NFTs ("burning") 
  * ‚ùå| this specification, NOT included‚ùå
  * if your contract needs it -> implement these -- by -- OTHER means

* `function onERC721Received(address _operator, address _from, uint256 _tokenId, bytes _data) external returns(bytes4);`
  * `address _operator`
    * doubt about its necessity
    * if the operator was important -> operator could 
      * transfer the token -- to -- themself &
        * == redundant
        * Reason: üß†operator -- considered as -- temporary owner of the tokenüß†
      * send it /
        * == `from` address (TODO: ‚ùì)
        * operator acts
          * ‚ö†Ô∏èon their own accord‚ö†Ô∏è
            * -> üëÄsignificant -- to the -- token recipientüëÄ
          * ‚ùåNOT on behalf of the token holder‚ùå
  * `address _from`
    * üëÄsignificant -- to the -- token recipientüëÄ
  * uses
    * old deployed contracts / may inadvertently return 1 (== `true`)
      * if you return & check for a magic value -> able to distinguish actual affirmative responses vs vacuous `true`s responses

* ALTERNATIVES considered
  * 2-step ERC-20 style transaction
    * -> requirements 
      * transfer functions NEVER throw
      * ALL functions return a boolean / == success of the operation

### **ERC-165 Interface**

* uses
  * expose the ERC-721 smart contract's supported interfaces

* POSSIBLE FUTURE EIP
  * global registry of interfaces -- for -- contracts
  * would allow,
    * your ERC-721 implementation implement -- , by delegating to a SEPARATE contract, -- `ERC721Enumerable`, `ERC721Metadata`, or other interfaces 

### **Gas and Complexity** (regarding the [enumeration extension](#specification))

* ALLOWED
  * üí°implementations / manage [few, *arbitrarily large*] numbers of NFTsüí°
    * == ERC-721 standard scales 

* recommendations
  * üëÄif your application is able to grow -> | your code, avoid using `for`/`while` loops üëÄ
    * see CryptoKitties bounty issue \#4
    * -> 
      * your contract -- may be -- unable to scale
      * gas costs will rise over time WITHOUT bound

* XXXXERC721
  * contract / deployed | Testnet /
    * instantiates & tracks 340282366920938463463374607431768211456 DIFFERENT deeds (== 2^128)
      * enough to
        * assign EVERY IPV6 address | Ethereum account owner
        * track ownership of nanobots / few micron in size
        * aggregate totalling half the size of Earth
    * can be queried it -- from the -- blockchain
    * gas / taken by every function << querying the ENS

* ALTERNATIVES considered
  * if it requires a for-loop -> remove the asset [enumeration function](#specification)
  * FROM enumeration functions -> return a Solidity array type 

#### **Privacy**

* ‚ùåNOT possible to get‚ùå
  * Reason: üß†attacker can simply (!) -- call -- `ownerOf` / EVERY POSSIBLE `tokenId`üß†
  * NOR, NFTs are NOT enumerable
    * _Example:_
      * private registry of property ownership
      * partially-private registry

#### **Metadata Choices** ([metadata extension](#metadata-extension))

* `name()` & `symbol()`
  * functions /
    * ALLOWED responses
      * empty string
      * == *your* contract

* OUTSIDE the scope of this standard,
  * how a client -- may determine -- the ERC-721 smart contracts / are well-known (== canonical) 

* `function tokenURI(uint256 _tokenId) external view returns (string);`
  * allows
    * NFTs -- are associated with -- URIs
  * image size == Instagram's recommendation
  * URI MAY be mutable
    * == changes from time to time
    * _Example:_ let's be NFT / represent ownership of a house
      * metadata about the house (image, occupants, etc.) can naturally change

* Metadata's functions return a string value
  * uses
    * call -- from -- `web3`
  * ‚ùåNOT uses
    * from OTHER contracts‚ùå

* ALTERNATIVES considered
  * put ALL asset's metadata | blockchain 
    * too expensive
  * use URL templates -- to -- query metadata parts 
    * URL templates do NOT work with ALL URL schemes
      * _Example:_ P2P URLs
  * multiaddr network address
    * NOT mature enough

#### **Community Consensus**

* MANY discussionS about the original ERC-721 issue
* participants
  - [@ImAllInNow](https://github.com/imallinnow) Rob from DEC Gaming / Presenting Michigan Ethereum Meetup Feb 7
  - [@Arachnid](https://github.com/arachnid) Nick Johnson
  - [@jadhavajay](https://github.com/jadhavajay) Ajay Jadhav from AyanWorks
  - [@superphly](https://github.com/superphly) Cody Marx Bailey - XRAM Capital / Sharing at hackathon Jan 20 / UN Future of Finance Hackathon.
  - [@fulldecent](https://github.com/fulldecent) William Entriken

## Backwards Compatibility

* FROM ERC-20 specification,
  * üëÄadoptedüëÄ
    * `balanceOf`,
    * `totalSupply`,
    * `name`
    * `symbol` 

* if ERC-721 implementation wants to be MORE compatible with ERC-20 -> include `decimals()` / returns `uint8(0)`

* _Example of NFT implementations | February 2018:_
  - CryptoKitties 
    - Compatible with this standard's earlier version
  - CryptoPunks
    - Partially ERC-20 compatible / NOT easily generalizable 
      - Reason: üß†
        - includes auction functionality DIRECTLY | contract
        - uses function names / explicitly -- refer to the -- assets as "punks"
  - Auctionhouse Asset Interface üß†
    - goal
      - generic interface -- for the -- Auctionhouse √êApp
    - "Asset" contract
      - very simple
      - miss 
        - ERC-20 compatibility,
        - `approve()` functionality,
        - metadata

* "Limited edition, collectible tokens"
  * ‚ùåNOT distinguishable assets‚ùå
  * == individual fungible tokens /
    * EACH one -- are tracked by -- its own smart contract / have own total supply
  * _Examples:_ Curio Cards, Rare Pepe

## Test Cases

* 0xcert ERC-721 Token
  * test cases -- via -- Truffle

## Implementations

* 0xcert ERC721 
  * reference implementation

* Su Squares
  * advertising platform | rent space & place images
  * implements the COMPLETE standard & ALL OPTIONAL interfaces

* ERC721ExampleDeed
  * example implementation -- via -- OpenZeppelin

* XXXXERC721, by William Entriken
  * scalable example implementation
  * deployed | testnet / 1 billion assets
    * == scaling is NOT a problem
  * support ALL lookups -- via -- metadata extension

## References

**Standards**

1. [ERC-20](./eip-20.md) Token Standard.
1. [ERC-165](./eip-165.md) Standard Interface Detection.
1. [ERC-173](./eip-173.md) Owned Standard.
1. [ERC-223](https://github.com/ethereum/EIPs/issues/223) Token Standard.
1. [ERC-677](https://github.com/ethereum/EIPs/issues/677) `transferAndCall` Token Standard.
1. [ERC-827](https://github.com/ethereum/EIPs/issues/827) Token Standard.
1. Ethereum Name Service (ENS). https://ens.domains
1. Instagram -- What's the Image Resolution? https://help.instagram.com/1631821640426723
1. JSON Schema. https://json-schema.org/
1. Multiaddr. https://github.com/multiformats/multiaddr
1. RFC 2119 Key words for use in RFCs to Indicate Requirement Levels. https://www.ietf.org/rfc/rfc2119.txt

* **Issues**
  1. The Original ERC-721 Issue. https://github.com/ethereum/eips/issues/721
  2. Solidity Issue \#2330 -- Interface Functions are External. https://github.com/ethereum/solidity/issues/2330
  3. Solidity Issue \#3412 -- Implement Interface: Allow Stricter Mutability. https://github.com/ethereum/solidity/issues/3412
  4. Solidity Issue \#3419 -- Interfaces Can't Inherit. https://github.com/ethereum/solidity/issues/3419
  5. Solidity Issue \#3494 -- Compiler Incorrectly Reasons About the `selector` Function. https://github.com/ethereum/solidity/issues/3494
  6. Solidity Issue \#3544 -- Cannot Calculate Selector of Function Named `transfer`. https://github.com/ethereum/solidity/issues/3544
  7. CryptoKitties Bounty Issue \#4 -- Listing all Kitties Owned by a User is `O(n^2)`. https://github.com/axiomzen/cryptokitties-bounty/issues/4
  8. OpenZeppelin Issue \#438 -- Implementation of `approve` method violates ERC20 standard. https://github.com/OpenZeppelin/zeppelin-solidity/issues/438
  9. Solidity DelegateCallReturnValue Bug. https://solidity.readthedocs.io/en/develop/bugs.html#DelegateCallReturnValue

* **Discussions**
  1. Reddit (announcement of first live discussion). https://www.reddit.com/r/ethereum/comments/7r2ena/friday_119_live_discussion_on_erc_nonfungible/
  2. Gitter #EIPs (announcement of first live discussion). https://gitter.im/ethereum/EIPs?at=5a5f823fb48e8c3566f0a5e7
  3. ERC-721 (announcement of first live discussion). https://github.com/ethereum/eips/issues/721#issuecomment-358369377
  4. ETHDenver 2018. https://ethdenver.com

* **NFT Implementations & OTHER Projects**
  1. CryptoKitties. https://www.cryptokitties.co
  2. 0xcert ERC-721 Token. https://github.com/0xcert/ethereum-erc721
  3. Su Squares. https://tenthousandsu.com
  4. Decentraland. https://decentraland.org
  5. CryptoPunks. https://www.larvalabs.com/cryptopunks
  6. DMarket. https://www.dmarket.io
  7. Enjin Coin. https://enjincoin.io
  8. Ubitquity. https://www.ubitquity.io
  9. Propy. https://tokensale.propy.com
  10. CryptoKitties Deployed Contract. https://etherscan.io/address/0x06012c8cf97bead5deae237070f9587f8e7a266d#code
  11. Su Squares Bug Bounty Program. https://github.com/fulldecent/su-squares-bounty
  12. XXXXERC721. https://github.com/fulldecent/erc721-example
  13. ERC721ExampleDeed. https://github.com/nastassiasachs/ERC721ExampleDeed
  14. Curio Cards. https://mycuriocards.com
  15. Rare Pepe. https://rarepepewallet.com
  16. Auctionhouse Asset Interface. https://github.com/dob/auctionhouse/blob/master/contracts/Asset.sol
  17. [OpenZeppelin `SafeERC20.sol` Implementation](../assets/eip-721/SafeERC20.sol).

## Copyright

Copyright and related rights waived via [CC0](../LICENSE.md).
