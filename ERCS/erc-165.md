---
eip: 165
title: Standard Interface Detection
author: Christian Reitwie√üner <chris@ethereum.org>, Nick Johnson <nick@ethereum.org>, Fabian Vogelsteller <fabian@lukso.network>, Jordi Baylina <jordi@baylina.cat>, Konrad Feldmeier <konrad.feldmeier@brainbot.com>, William Entriken <github.com@phor.net>
type: Standards Track
category: ERC
status: Final
created: 2018-01-23
requires: 214
---

## Simple Summary

* goal
  * creates a standard method -- to -- publish & detect the interfaces / implemented by a smart contract

## Abstract

* standardize
  1. How to identify interfaces
  2. How a contract -- will publish the -- interfaces / implements
  3. How to detect if a contract -- implements -- ERC-165
  4. How to detect if a contract --- implements -- any given interface

## Motivation

* | [ERC-20 token interface](./eip-20.md),
  * query whether a contract -- supports the -- interface & used version

## Specification

### How to identify Interfaces?

* | this standard,
  * interface 
    * == set of [function selectors -- as defined by the -- Ethereum ABI](https://solidity.readthedocs.io/en/develop/abi-spec.html#function-selector)
    * == subset of [Solidity's concept of interfaces](https://solidity.readthedocs.io/en/develop/abi-spec.html) & `interface` keyword
    * XOR of ALL function selectors | interface
    * ‚ùåNOT permit optional functions‚ùå
    * _Example:_ how to calculate an interface identifier
      ```solidity
      pragma solidity ^0.4.20;
      
      interface Solidity101 {
          function hello() external pure;
          function world(int) external pure;
      }
      
      contract Selector {
          function calculateSelector() public pure returns (bytes4) {
              Solidity101 i;
              return i.hello.selector ^ i.world.selector;
          }
      }
      ```

### How a contract / implements interfaces, publish them?

* contract / ERC-165 compliant
  * üëÄshall implement `ERC165.sol` üëÄ
    ```solidity
    pragma solidity ^0.4.20;
    
    interface ERC165 {
        /// @notice Query if a contract implements an interface
        /// @param interfaceID The interface identifier, as specified in ERC-165
        /// @dev Interface identification is specified in ERC-165. This function
        ///  uses less than 30,000 gas.
        /// @return `true` if the contract implements `interfaceID` and
        ///  `interfaceID` is not 0xffffffff, `false` otherwise
        function supportsInterface(bytes4 interfaceID) external view returns (bool);
    }
    ```
    * `0x01ffc9a7`
      * interface identifier
      * calculated -- by -- running `bytes4(keccak256('supportsInterface(bytes4)'));` /
        * `supportsInterface` function
          * about returns
            - if `interfaceID` == `0x01ffc9a7` (EIP165 interface) -> returns `true` 
            - if `interfaceID` == `0xffffffff` -> returns `false`
            - if `interfaceID` == OTHER value / contract implements -> returns `true`
            - otherwise -> returns `false`
          * use <= 30,000 gas
          * EXIST SEVERAL logical ways -- to -- implement it

### How to detect if a contract implements ERC-165?

1. source contract makes a `STATICCALL` -- to the -- destination address / input data
   1. `0x01ffc9a701ffc9a700000000000000000000000000000000000000000000000000000000` & gas 30,000
   2. == `contract.supportsInterface(0x01ffc9a7)`
2. if the call
   1. fails OR return `false` -> destination contract does NOT implement ERC-165
   2. returns `true` -> second call is made -- with -- input data == `0x01ffc9a7ffffffff00000000000000000000000000000000000000000000000000000000`
      1. if the second call
         1. fails or returns `false` -> destination contract does NOT implement ERC-165
         2. OTHERWISE -> implements ERC-165

### How to detect if a contract implements ANY given interface?

1. check if the [contract implements ERC-165](#how-to-detect-if-a-contract-implements-erc-165)
2. if it 
   1. does NOT implement ERC-165 -> see what methods use the old-fashioned way
   2. implements ERC-165 & you want to check if it implements `interfaceID` -> call `supportsInterface(interfaceID)`

## Rationale

* this specification's goal
  * as simple as possible
  * compatible -- with the -- current Solidity version

## Backwards Compatibility

* `0xffffffff` mechanism
  * allows  
    * üëÄchecking if a contract / created BEFORE this standard -- implement -- ERC-165üëÄ

* this EIP
  * ALREADY implemented -- by --[ENS](./eip-137.md)  

## Test Cases

* contract / 
  * üí°detects the interfaces / OTHER contracts implementüí°
    ```solidity
    pragma solidity ^0.4.20;
  
    contract ERC165Query {
        bytes4 constant InvalidID = 0xffffffff;
        bytes4 constant ERC165ID = 0x01ffc9a7;
  
        function doesContractImplementInterface(address _contract, bytes4 _interfaceId) external view returns (bool) {
            uint256 success;
            uint256 result;
  
            (success, result) = noThrowCall(_contract, ERC165ID);
            if ((success==0)||(result==0)) {
                return false;
            }
  
            (success, result) = noThrowCall(_contract, InvalidID);
            if ((success==0)||(result!=0)) {
                return false;
            }
  
            (success, result) = noThrowCall(_contract, _interfaceId);
            if ((success==1)&&(result==1)) {
                return true;
            }
            return false;
        }
  
        function noThrowCall(address _contract, bytes4 _interfaceId) constant internal returns (uint256 success, uint256 result) {
            bytes4 erc165ID = ERC165ID;
  
            assembly {
                    let x := mload(0x40)               // Find empty storage location using "free memory pointer"
                    mstore(x, erc165ID)                // Place signature at beginning of empty storage
                    mstore(add(x, 0x04), _interfaceId) // Place first argument directly next to signature
  
                    success := staticcall(
                                        30000,         // 30k gas
                                        _contract,     // To addr
                                        x,             // Inputs are stored at location x
                                        0x24,          // Inputs are 36 bytes long
                                        x,             // Store output over input (saves space)
                                        0x20)          // Outputs are 32 bytes long
  
                    result := mload(x)                 // Load the result
            }
        }
    }
    ```

## Implementation

* `ERC165MappingImplementation` 
  * contract / 
    * generic & reusable
    * `view` function -- implementation of -- `supportsInterface`
      * execution cost / ANY input == 586 gas
        * ‚ö†Ô∏ècontract initialization -- requires -- storing EACH interface
          * == `SSTORE` == 20,000 gas‚ö†Ô∏è
  ```solidity
  pragma solidity ^0.4.20;
  
  import "./ERC165.sol";
  
  contract ERC165MappingImplementation is ERC165 {
      /// @dev You must not set element 0xffffffff to true
      mapping(bytes4 => bool) internal supportedInterfaces;
  
      function ERC165MappingImplementation() internal {
          supportedInterfaces[this.supportsInterface.selector] = true;
      }
  
      function supportsInterface(bytes4 interfaceID) external view returns (bool) {
          return supportedInterfaces[interfaceID];
      }
  }
  
  interface Simpson {
      function is2D() external returns (bool);
      function skinColor() external returns (string);
  }
  
  contract Lisa is ERC165MappingImplementation, Simpson {
      function Lisa() public {
          supportedInterfaces[this.is2D.selector ^ this.skinColor.selector] = true;
      }
  
      function is2D() external returns (bool){}
      function skinColor() external returns (string){}
  }
  ```

* `Homer`
  * contract /
    * `pure` (TODO: Where ‚ùì) function implementation -- of -- `supportsInterface`
      * ‚ö†Ô∏èexecution cost / increases linearly -- based on -- number of supported interfaces ‚ö†Ô∏è
        * worst-case cost == 236 gas 

  ```solidity
  pragma solidity ^0.4.20;
  
  import "./ERC165.sol";
  
  interface Simpson {
      function is2D() external returns (bool);
      function skinColor() external returns (string);
  }
  
  contract Homer is ERC165, Simpson {
      function supportsInterface(bytes4 interfaceID) external view returns (bool) {
          return
            interfaceID == this.supportsInterface.selector || // ERC165
            interfaceID == this.is2D.selector
                           ^ this.skinColor.selector; // Simpson
      }
  
      function is2D() external returns (bool){}
      function skinColor() external returns (string){}
  }
  ```

* | \>=3 supported interfaces (as ERC165),
  * mapping approach costs 's gas << pure approach's cost's gas

## Version history
* [PR 1640](https://github.com/ethereum/EIPs/pull/1640)
  * corrects the `noThrowCall` test case -- to -- use 36 bytes / != PREVIOUS 32 bytes
  * PREVIOUS code
    * | Solidity 0.4.x,
      * silently worked
    * | Solidity 0.5.0,
      * was broken -- by -- NEW behavior

## Copyright

Copyright and related rights waived via [CC0](../LICENSE.md).
